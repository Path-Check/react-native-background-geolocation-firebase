apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION     = 28
def DEFAULT_BUILD_TOOLS_VERSION     = "28.0.3"
def DEFAULT_TARGET_SDK_VERSION      = 28

def DEFAULT_FIREBASE_CORE_VERSION    = "16.0.9"
def DEFAULT_FIREBASE_FIRESTORE_VERSION = "19.0.0"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)

    defaultConfig {
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
}

rootProject.gradle.buildFinished { buildResult ->
    if (buildResult.getFailure() != null) {
        try {
            String message = buildResult.getFailure().properties.get("reportableCauses").toString()

            if (message.contains("DexArchiveMergerException") || message.contains("DexIndexOverflowException")) {
                logger.log(LogLevel.ERROR, "")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
                logger.log(LogLevel.ERROR, "|         react-native-background-geolocation-firebase     | ")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
                logger.log(LogLevel.ERROR, "|                                                          | ")
                logger.log(LogLevel.ERROR, "| In your app/build.gradle, enable multidex:               | ")
                logger.log(LogLevel.ERROR, "|                                                          | ")
                logger.log(LogLevel.ERROR, "| android {                                                | ")
                logger.log(LogLevel.ERROR, "|    defaultConfig {                                       | ")
                logger.log(LogLevel.ERROR, "|        multiDexEnabled true  // <-- Add this             | ")
                logger.log(LogLevel.ERROR, "|    }                                                     | ")
                logger.log(LogLevel.ERROR, "| }                                                        | ")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")

            }
        } catch (Exception exception) {

        }
    }
}

repositories{
    maven {
        url './libs'
    }
}

dependencies {
    def firebaseCoreVersion = rootProject.hasProperty('firebaseCoreVersion') ? rootProject.firebaseCoreVersion : DEFAULT_FIREBASE_CORE_VERSION
    def firebaseFirestoreVersion = rootProject.hasProperty('firebaseFirestoreVersion') ? rootProject.firebaseFirestoreVersion : DEFAULT_FIREBASE_FIRESTORE_VERSION

    implementation 'com.facebook.react:react-native:+'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.google.firebase:firebase-firestore:$firebaseFirestoreVersion"
    implementation(group: 'com.transistorsoft', name:'tslocationmanager', version: '+')
    implementation(group: 'com.transistorsoft', name:'tsfirebaseproxy', version: '+')
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.3.0-2'
}
